plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.11'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.example.book'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

task copyFrontEndAssets(type: Copy) {
    from 'frontend/dist'
    into 'src/main/resources/static'
    exclude 'index.html'
}

task copyFrontEndTemplate(type: Copy) {
    from 'frontend/dist'
    into 'src/main/resources/templates'
    include 'index.html'
}

task copyFrontEndAssetsToTarget(type: Copy) {
    from 'frontend/dist'
    into 'target/classes/static'
    exclude 'index.html'
}

task copyFrontEndTemplateToTarget(type: Copy) {
    from 'frontend/dist'
    into 'target/classes/templates'
    include 'index.html'
}

task installFrontEnd(type: Exec) {
    workingDir file("${projectDir}/frontend")
    commandLine 'npm', 'install'
}

task testFrontEnd(type: Exec) {
    workingDir file("${projectDir}/frontend")
    commandLine 'npm', 'run', 'test:unit'
}

task buildFrontEnd(type: Exec) {
    workingDir file("${projectDir}/frontend")
    commandLine 'npm', 'run', 'build'
}

clean {
    delete 'src/main/resources/static/static'
    delete 'src/main/resources/templates/index.html'
}

bootJar {
    dependsOn copyFrontEndAssetsToTarget, copyFrontEndTemplateToTarget
}
